%description:
Test reference to:
     parent module parameter,
     local submodule parameter,
     other submodule's parameter
from
     module parameter assignment,
     gatesize assignment,
     module vector size expression

%file: test.ned

import testlib.Dump;

module Node
{
    parameters:
        int p;
        int a = default(111);
        int b = default(222);
        int c = default(333);
        int d = default(444);
    gates:
        input ga[];
        input gb[];
        input gc[];
        input gd[];
    connections allowunconnected:
}

module Nil
{
}

network Test
{
    parameters:
        int p = 1;
    submodules:
        n: Node {
            parameters:
                p = 2;
        }
        m: Node {
            parameters:
                p = 3;
                // from parameter assignment:
                a = p;      // ~ this.p
                b = this.p;
                c = parent.p;
                d = parent.n.p;
            gates:
                // from gate vector size:
                ga[p];      // ~ this.p
                gb[this.p];
                gc[parent.p];
                gd[parent.n.p];
        };

        // from module vector size:
        ma[p]: Nil;      // compound module's parameter
        mb[this.p]: Nil; // compound module's parameter (!!!)
        mc[n.p]: Nil;    // n submodule's parameter
        //md[parent.p]: Nil; ==> ERROR (refers to compound module's parent which it doesn't have)
        //me[parent.n.p]: Nil; ==> ERROR (refers to compound module's parent which it doesn't have)

        // from ini file
        ini: Node;

        dump: Dump;

    connections allowunconnected:
}

%inifile: test.ini
[General]
network = Test
cmdenv-express-mode = false
cmdenv-event-banners = false

*.ini.p = 4
*.ini.a = p          # local module's parameter
*.ini.b = this.p     # local module's parameter
*.ini.c = parent.p   # parent's parameter
*.ini.d = parent.n.p # other submodule's parameter

#Note: default params will be assigned automatically

%contains: stdout
module Test: Test {
    parameters:
        @isNetwork
        p = 1
    submodules:
        module Test.n: Node {
            parameters:
                p = 2
                a = 111
                b = 222
                c = 333
                d = 444
        }
        module Test.m: Node {
            parameters:
                p = 3
                a = 3
                b = 3
                c = 1
                d = 2
            gates:
                ga[0]: not connected
                ga[1]: not connected
                ga[2]: not connected
                gb[0]: not connected
                gb[1]: not connected
                gb[2]: not connected
                gc[0]: not connected
                gd[0]: not connected
                gd[1]: not connected
        }
        module Test.ma[0]: Nil {
        }
        module Test.mb[0]: Nil {
        }
        module Test.mc[0]: Nil {
        }
        module Test.mc[1]: Nil {
        }
        module Test.ini: Node {
            parameters:
                p = 4
                a = 4
                b = 4
                c = 1
                d = 2
        }
}

