%description:
Test JSON-style object parameters.

%file: test.ned

import testlib.Dump;

network Test {
  parameters:
    object nullptr1 = null;
    object nullptr2 = nullptr;
    object array1 = [];
    object array2 = [ 1s, 1+1, 3.14157m, true, false, "Hello", 'World' ];
    object array3 = [ nan, inf, inf s, null, nullptr ];
    object array4 = [[1,2,3], [4,5,6]];
    object array5 = [{foo:1,bar:2}, {FOO:10,BAR:20}];
    object object1 = {};
    object object2 = { f1: 100, f2: 200, f3: "hello", f4: "hello"+"!", f5: null, f6: nullptr };
    object object3 = { "f1": 100, 'f2': 200, "f3": "hello", 'f4': nullptr };
    object object4 = {arr1 : [1,2,3], arr2 : [4,5,6]};
    object object5 = {obj1 : {foo:1,bar:2}, obj2 : {FOO:10,BAR:20}};
    object objkeys1 = {  3.14: 1,  1e6: 2,  42: 3,  5.1GHz : 4 };
    object objkeys2 = { -3.14: 1, -1e6: 2, -42: 3, -5.1GHz : 4 };
    object objkeys3 = { nan: 1, inf: 2, true: 4, false: 5 };
    object objkeys4 = { -inf: 3 };
    object objkeys5 = { null: 5, nullptr: 6 };
    object extracomma1 = [ 1, 2, ];
    object extracomma2 = { a:1, b:2, };

  submodules:
    dump: Dump;
}

%contains: stdout
module Test: Test {
    parameters:
        @isNetwork
        nullptr1 = nullptr
        nullptr2 = nullptr
        array1 = []
        array2 = [1s, 2, 3.14157m, true, false, "Hello", "World"]
        array3 = [nan, inf, inf s, nullptr, nullptr]
        array4 = [[1, 2, 3], [4, 5, 6]]
        array5 = [{bar: 2, foo: 1}, {BAR: 20, FOO: 10}]
        object1 = {}
        object2 = {f1: 100, f2: 200, f3: "hello", f4: "hello!", f5: nullptr, f6: nullptr}
        object3 = {f1: 100, f2: 200, f3: "hello", f4: nullptr}
        object4 = {arr1: [1, 2, 3], arr2: [4, 5, 6]}
        object5 = {obj1: {bar: 2, foo: 1}, obj2: {BAR: 20, FOO: 10}}
        objkeys1 = {1e6: 2, 3.14: 1, 42: 3, 5.1 GHz: 4}
        objkeys2 = {-1e6: 2, -3.14: 1, -42: 3, -5.1 GHz: 4}
        objkeys3 = {false: 5, inf: 2, nan: 1, true: 4}
        objkeys4 = {-inf: 3}
        objkeys5 = {null: 5, nullptr: 6}
        extracomma1 = [1, 2]
        extracomma2 = {a: 1, b: 2}
    submodules:
}

%not-contains: stdout
undisposed object
